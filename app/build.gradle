apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: "../scripts/cling.gradle"

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        applicationId "com.m3sv.plainupnp"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
    }

    def isDeploy = System.getenv('IS_DEPLOY')

    if (isDeploy)
        signingConfigs {
            release {
                keyAlias System.getenv('KEY_ALIAS')
                keyPassword System.getenv('KEY_PASSWORD')
                storeFile file(System.getenv('STORE_FILE'))
                storePassword System.getenv('STORE_PASSWORD')
            }
        }

    buildTypes {
        release {
            minifyEnabled true
            if (isDeploy)
                signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    compileOnly deps.dagger.annotation

    implementation deps.androidx.appCompat
    implementation deps.androidx.cardView
    implementation deps.androidx.constraintLayout
    implementation deps.ktx.core
    implementation deps.androidx.lifecycle.extensions
    implementation deps.androidx.lifecycle.lifecycleKtx
    implementation deps.androidx.lifecycle.liveDataKtx
    implementation deps.androidx.lifecycle.liveDataKtx
    implementation deps.androidx.lifecycle.viewModelKtx
    implementation deps.androidx.material
    implementation deps.androidx.preference
    implementation deps.androidx.recyclerView
    implementation deps.coroutines.core
    implementation deps.dagger.core
    implementation deps.glide
    implementation deps.kotlin
    implementation deps.nanohttpd.core
    implementation deps.nanohttpd.webserver
    implementation deps.sqlDelightAndroidDriver
    implementation deps.timber

    implementation project(':common')
    implementation project(':core-persistence')
    implementation project(':core-event-bus')
    implementation project(':core-resources')
    implementation project(':feature-home')
    implementation project(':feature-onboarding')
    implementation project(':feature-settings')
    implementation project(':feature-in-app-player')
    implementation project(':shared-upnp')

    kapt deps.dagger.compiler

    testImplementation deps.androidx.coreTesting
    testImplementation deps.coroutines.test
    testImplementation deps.test.junit
    testImplementation deps.test.mockito
}
