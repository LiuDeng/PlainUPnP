buildscript {
    ext.buildConfig = [
            'compileSdk': 29,
            'minSdk'    : 21,
            'targetSdk' : 29,
            'buildTools': '29.0.3',
            'version'   : [
                    'major': 2,
                    'minor': 8,
                    'patch': 2,
                    'build': 0,
                    'code' : 68
            ],
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"

    ext.versions = [
            kotlin                : '1.4.21',
            coreLibraryDesugaring : '1.1.1',
            sqlDelight            : '1.4.4',
            architectureComponents: "2.2.0",
            cardView              : "1.0.0",
            cling                 : "2.2.0",
            constraintLayout      : "2.0.0",
            coroutines            : "1.3.9",
            coreTesting           : "2.1.0",
            dagger                : "2.27",
            detekt                : "1.0.0",
            hilt                  : "2.28-alpha",
            jetty                 : "8.1.8.v20121106",
            okHttp                : "4.9.0",
            nanohttpd             : "2.3.1",
            recyclerView          : "1.1.0",
            supportLibrary        : "1.1.0",
            test                  : "1.2.0"
    ]
    ext.deps = [
            'javaxCdi'               : "javax.enterprise:cdi-api:2.0.SP1",
            'kotlin'                 : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
            'glide'                  : "com.github.bumptech.glide:glide:4.11.0",
            'fastScroll'             : 'me.zhanghai.android.fastscroll:library:1.1.3',
            'photoView'              : 'com.github.chrisbanes:PhotoView:2.3.0',
            'exoPlayer'              : 'com.google.android.exoplayer:exoplayer:2.12.0',
            'okHttp'                 : "com.squareup.okhttp3:okhttp:${versions.okHttp}",
            'sqlDelightAndroidDriver': "com.squareup.sqldelight:android-driver:${versions.sqlDelight}",
            'timber'                 : 'com.jakewharton.timber:timber:4.7.1',
            'androidx'               : [

                    'material'        : 'com.google.android.material:material:1.2.0',
                    'ktx'             : [
                            "core": "androidx.core:core-ktx:1.3.2"
                    ],
                    "appCompat"       : "androidx.appcompat:appcompat:${versions.supportLibrary}",
                    "cardView"        : "androidx.cardview:cardview:${versions.cardView}",
                    "constraintLayout": "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}",
                    "coreTesting"     : "androidx.arch.core:core-testing:${versions.coreTesting}",
                    "lifecycle"       : [
                            "extensions"  : "androidx.lifecycle:lifecycle-extensions:${versions.architectureComponents}",
                            "liveDataKtx" : "androidx.lifecycle:lifecycle-livedata-ktx:${versions.architectureComponents}",
                            "viewModelKtx": "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.architectureComponents}",
                            "lifecycleKtx": "androidx.lifecycle:lifecycle-runtime-ktx:${versions.architectureComponents}"
                    ],
                    "preference"      : "androidx.preference:preference:${versions.supportLibrary}",
                    "recyclerView"    : "androidx.recyclerview:recyclerview:${versions.recyclerView}"
            ],
            'coroutines'             : [
                    "core": "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}",
                    "test": "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.coroutines}"
            ],
            'dagger'                 : [
                    "annotation": "javax.inject:javax.inject:1",
                    "compiler"  : "com.google.dagger:dagger-compiler:${versions.dagger}",
                    "core"      : "com.google.dagger:dagger:${versions.dagger}"
            ],
            'nanohttpd'              : [
                    "core"     : "org.nanohttpd:nanohttpd:${versions.nanohttpd}",
                    "webserver": "org.nanohttpd:nanohttpd-webserver:${versions.nanohttpd}"
            ],
            'test'                   : [
                    "junit"        : "org.junit.jupiter:junit-jupiter:5.6.2",
                    "mockito"      : "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0",
                    "mockitoInline": "org.mockito:mockito-inline:2.13.0"
            ]
    ]

    repositories {
        google()
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.squareup.sqldelight:gradle-plugin:${versions.sqlDelight}"
    }
}

plugins {
    id "com.github.plnice.canidropjetifier" version "0.5"
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = JavaVersion.VERSION_1_8
        task.targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
        task.kotlinOptions {
            freeCompilerArgs += [
                    '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                    '-Xopt-in=kotlinx.coroutines.ObsoleteCoroutinesApi',
                    '-Xopt-in=kotlin.time.ExperimentalTime',
                    '-Xopt-in=kotlinx.coroutines.FlowPreview',
                    '-Xopt-in=kotlin.ExperimentalStdlibApi'
            ]
        }
    }
}
